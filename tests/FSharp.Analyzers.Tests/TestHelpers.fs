module Criipto.FSharp.Analyzers.Tests.TestHelpers

open System.Collections.Generic
open System.IO
open System.Text.Json
open System.Text.Json.Serialization

open Xunit
open Snapshooter
open Snapshooter.Xunit

open FSharp.Compiler.Text
open FSharp.Analyzers.SDK
open FSharp.Analyzers.SDK.Testing


// This is necessary because the Range type is self-referencing and thus unrenderable with the default setup
module RangeHack =
    type RangeRendition = {
        End: Position
        EndColumn: int
        EndLine: int
        FileName: string
        Start: Position
        StartColumn: int
        StartLine: int
    }

    type MessageRendition = {
        Code: string
        Fixes: Fix list
        Message: string
        Range : RangeRendition
        Severity: Severity
        Type: string
    }

    let private renderRange (range: Range) : RangeRendition = {
        End = range.End
        EndColumn = range.EndColumn
        EndLine = range.EndLine
        FileName = range.FileName
        Start = range.Start
        StartColumn = range.StartColumn
        StartLine = range.StartLine
    }

    let renderMessage (msg : Message) : MessageRendition = {
        Code = msg.Code
        Fixes = msg.Fixes
        Message = msg.Message
        Range = renderRange msg.Range
        Severity = msg.Severity
        Type = msg.Type
    }

type TestFiles =
    static member private dataFolder = Path.Combine [|__SOURCE_DIRECTORY__; "data" |]

    /// Gets the source code and file names of all test programs in the specified directory.
    static member GetSources (directory: string) : IEnumerable<string[]> =
        let dir = Path.Combine [|TestFiles.dataFolder; directory |]
        let getSource filename = filename |> File.ReadAllText
        let testName filename = Path.GetRelativePath(dir, filename)
        let data filename = [| getSource filename; testName filename |]

        let filenames = Directory.GetFiles dir
        Array.map data filenames

let jsonOptions =  JsonFSharpOptions.Default().ToJsonSerializerOptions()

/// Serializes the messages generated by an analyzer and takes a snapshot of the serialization.
let private snapshotMessages (snapshotName: SnapshotFullName) (messages: Message list) =
    let renderedMessages = List.map RangeHack.renderMessage messages
    let rawMessage = JsonSerializer.Serialize(renderedMessages, jsonOptions)

    Snapshot.Match(rawMessage, snapshotName)

/// Tests that the given analyzer:
/// - Produces at least one message when run on the given program
/// - Produces the same messages as in the snapshot for the given program.
///
/// The snapshotName must be passed in by the caller due to limitations in Snapshooter.
let runPositiveTest snapshotName setupContext analyzer program filename = async {
    let! opts = setupContext()
    let ctx = getContext opts program
    let! msgs = analyzer ctx
    Assert.NotEmpty msgs
    snapshotMessages snapshotName msgs
}

/// Tests that the given analyzer produces no messages for the given program.
let runNegativeTest setupContext (analyzer : CliContext -> Async<Message list>) program = async {
    let! opts = setupContext()
    let ctx = getContext opts program
    let! msgs = analyzer ctx
    Assert.Empty msgs
}